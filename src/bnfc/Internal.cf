-- Grammar of the Vehicle Internal language

comment "(*" "*)";

-- * Tokens

position token BuiltinToken
  ( {"forall"} | {"exists"}
  | {"=>"} | {"and"} | {"or"} | {"not"}
  | {"=="} | {"!="} | {"<="} | {"<"} | {">="} | {">"}
  | {"*"} | {"/"} | {"+"} | {"-"} | {"~"}
  | {"Nat"} | {"Int"} | {"Real"} | {"Index"}
  | {"Prop"} | {"Bool"}
  | {"Tensor"} | {"List"} | {"::"} | {"!"}
  | {"Eq"} | {"Ord"} | {"Container"} | {"Truth"}
  | {"NatOps"} | {"IntOps"} | {"RatOps"}
  );
position token NameToken (lower (letter | digit | '_')*) ;
position token BoolToken ({"True"} | {"False"}) ;
position token HoleToken ({"?"} (letter | digit | '_')*);
position token TypeToken ({"Type"} digit+);

position token Natural (digit+ '.' digit+);
position token Rational (digit+ '.' digit+);

-- * Binders

ExplicitBinder. Binder ::= "("  NameToken ":type" Expr ")";
ImplicitBinder. Binder ::= "{"  NameToken ":type" Expr "}";
InstanceBinder. Binder ::= "{{" NameToken ":type" Expr "}}";

-- * Function arguments

ExplicitArg. Arg ::= Expr1;
ImplicitArg. Arg ::= "{" Expr "}";
InstanceArg. Arg ::= "{{" Expr "}}";

-- * Literals

LitNat.  Lit ::= Natural;
LitRat.  Lit ::= Rational;
LitBool. Lit ::= BoolToken;

-- * Core structure.

Ann.           Expr  ::= Expr1 ":type" Expr1;
Pi.            Expr  ::= "pi" Binder Expr1;
Let.           Expr  ::= "let" Binder Expr1 Expr1;
Lam.           Expr  ::= "lambda" Binder Expr1;
App.           Expr  ::= Expr1 Arg;
Type.          Expr1 ::= TypeToken;
Builtin.       Expr1 ::= BuiltinToken;
Var.           Expr1 ::= NameToken;
Literal.       Expr1 ::= Lit;
LSeq.          Expr1 ::= "[" [Expr1] "]";
Hole.          Expr1 ::= HoleToken;

coercions Expr 1;
separator Expr1 "";

-- * Declarations

DeclNetw.  Decl ::= "(" "declare-network"   NameToken Expr1 ")";
DeclData.  Decl ::= "(" "declare-dataset"   NameToken Expr1 ")";
DeclParam. Decl ::= "(" "declare-parameter" NameToken Expr1 ")";
DefFun.    Decl ::= "(" "define-fun"        NameToken Expr1 Expr1 ")";

separator Decl "";

-- * Program

Main. Prog ::= "(" [Decl] ")";

